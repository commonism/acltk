#include :: "ip.bnf"
#include :: "acl.bnf"

grammar
    =
    @:{command}+ ("line"|"end"|$)
    ;

version
	= "version" version:?/[^\n]*/? NL
	;

command
	= version
	| hostname
	| domain_name
	| interface
	| access_list
	| ip
	| ignored
	;

interface_detail
	= type:"description" WS value:TOEOL NL
	| type:("ip" WS "address") WS value:(ip4 WS ip4 [WS "secondary"]) NL
	| type:("ip" WS "access-group") WS value:(obj_name WS ("in"|"out")) NL
	| TOEOL NL
	| ()
	;

banner
    = "banner" WS banner_cmd
    ;


banner_cmd
    = "motd" WS delim_start delim_msg delim_stop NL
    ;


delim_start
    = "^C"
    | ?/./?
    ;

delim_msg
    = ()
    ;

delim_stop
    = ()
    ;

ip
	= "ip" WS cmd:ip_command
	;

ip_command
	= cmd:"access-list" WS object:ip_access_list
	| cmd:"domain name" WS name:obj_name
	| cmd:"route" WS route:ip_route
	| ip_ignored
	;

(*
	ip route [vrf vrf-name] prefix mask {ip-address | interface-type interface-number [ip-address]} [dhcp] [distance] [name next-hop-name] [permanent | track number] [tag tag]
	no ip route [vrf vrf-name] prefix mask {ip-address | interface-type interface-number [ip-address]} [dhcp] [distance] [name next-hop-name] [permanent | track number] [tag tag]
*)

ip_route
	= "profile"
	| prefix:ip4 WS mask:ip4 WS gw:(ip4|acl_interface_id) NL
	;

ip_access_list
	(* ip access-list { { standard | extended } { access-list-name | access-list-number } | helper egress check } *)
	= type:"standard" WS name:obj_name NL objects:{ip_access_list_standard}+
	| type:"extended" WS name:obj_name NL objects:{ip_access_list_extended}+
	;

ip_access_list_standard_rule_option
	= type:"log" [WS value:identifier]
	;

ip_access_list_standard_rule_options
	= {WS ip_access_list_standard_rule_option}*
	;

ip_access_list_remark
	= [seq:int WS] "remark" SP remark:remark NL
	;

ip_access_list_standard_rule
	= [seq:int WS] mode:acl_mode WS source:ios_host options:ip_access_list_standard_rule_options NL
	;

ios_host
	= address:ip4 [WS wildcard:ip4]
	| "host" WS address:ip4
	| address:"any"
	;

ios_node
	= host:ios_host [WS port:acl_port]
	;

ip_access_list_standard
	= remark:{SP ip_access_list_remark}+ SP >ip_access_list_standard_rule
	| SP >ip_access_list_standard_rule
	;

ip_access_list_extended_rule_option
	= type:"precedence" WS value:identifier
	| type:"dscp" WS value:identifier
	| type:"tos" WS value:identifier
	| type:"time-range" WS value:identifier
	| type:"fragments"
	| type:"log-input" [WS value:identifier]
	| type:"log" [WS value:identifier]
	| type:"established"
	;

ip_access_list_extended_rule_options
	= {WS ip_access_list_extended_rule_option}*
	;

ip_access_list_extended_rule
	= [seq:int WS] mode:acl_mode WS protocol:protocol_icmp WS source:ios_host WS dest:ios_host icmp:acl_icmp_options options:ip_access_list_extended_rule_options NL
	| [seq:int WS] mode:acl_mode WS protocol:acl_protocol WS source:ios_node WS dest:ios_node options:ip_access_list_extended_rule_options NL
	| [seq:int WS] mode:acl_mode WS protocol:acl_protocol WS source:ios_host WS dest:ios_host options:ip_access_list_extended_rule_options NL
	;

ip_access_list_extended
	= remark:{SP ip_access_list_remark}+ SP >ip_access_list_extended_rule
	| SP >ip_access_list_extended_rule
	;

host_wildcard
	= host:ip4 [WS wildcard:ip4]
	;

(* access-list access-list-number { deny | permit } source [source-wildcard] [ log [word] ]  *)
access_list_ip_standard_rule
	=  "access-list" WS id:access_list_ip_standard_id WS mode:acl_mode WS source:ios_host options:ip_access_list_standard_rule_options NL
	;

access_list_ip_standard
	= remark:{access_list_remark}+ >access_list_ip_standard_rule
	| >access_list_ip_standard_rule
	;

access_list_remark
	= 'access-list' WS acl_id WS 'remark' WS remark:remark NL
	;

access_list
	= access_list_ip_extended
	| access_list_ip_standard
	;

(*
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } protocol source source-wildcard destination destination-wildcard [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } icmp source source-wildcard destination destination-wildcard [ icmp-type [icmp-code] | icmp-message ] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } igmp source source-wildcard destination destination-wildcard [igmp-type] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } tcp source source-wildcard [ operator [port] ] destination destination-wildcard [ operator [port] ] [established] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } udp source source-wildcard [ operator [port] ] destination destination-wildcard [ operator [port] ] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
*)


access_list_ip_extended_rule
	= "access-list" WS id:access_list_ip_extended_id WS mode:acl_mode WS protocol:protocol_icmp WS source:acl_icmp_node WS dest:acl_icmp_node icmp:acl_icmp_options options:ip_access_list_extended_rule_options NL
	| "access-list" WS id:access_list_ip_extended_id WS mode:acl_mode WS protocol:acl_protocol  WS source:node WS dest:node options:ip_access_list_extended_rule_options NL
	;

access_list_ip_extended
	= remark:{access_list_remark}+ >access_list_ip_extended_rule
	| >access_list_ip_extended_rule
	;

access_list_ip_standard_id
	= /[1-9][0-9]?/ &WS
	| /13[0-9][0-9]/ &WS
	;

access_list_ip_extended_id
	= /1[0-9][0-9]/ &WS
	| /2[0-6][0-9][0-9]/ &WS
	;


ignored_indent
	= {/^ [^\n]*/ NL}*
	;


ignored
	= "!" TOEOL NL
	| ":" TOEOL NL
	| "aaa" TOEOL NL
	| "alias" TOEOL NL
	| "appfw" TOEOL NL ignored_indent
	| "archive" TOEOL NL ignored_indent
	| "arp" TOEOL NL
	| banner
	| ("boot-start-marker"|"boot-end-marker") NL
	| "boot" TOEOL NL
	| "bridge" TOEOL NL
	| "call-home" TOEOL NL ignored_indent
	| "class-map" WS TOEOL NL ignored_indent
	| "clock" TOEOL NL
	| "control-plane" NL
	| "crypto" TOEOL NL ignored_indent
	| "cts" TOEOL NL
	| "Current configuration" TOEOL NL
	| "define" WS "interface-range" TOEOL NL
	| "diagnostic" TOEOL NL
	| "dialer-list" TOEOL NL
	| "dial-peer" TOEOL NL
	| "dot11" TOEOL NL ignored_indent
	| "enable" TOEOL NL
	| "encryption" TOEOL NL
	| "errdisable" TOEOL NL
	| "firewall" TOEOL NL
	| "hw-module" TOEOL NL
	| "ipv6" TOEOL NL ignored_indent
	| "isdn" TOEOL NL
	| "key" TOEOL NL ignored_indent
	| "lldp" TOEOL NL
	| "logging" TOEOL NL
	| "mac-address-table" TOEOL NL
	| "mac-address" TOEOL NL
	| "mls" TOEOL NL
	| "mmi" TOEOL NL
	| "multilink" TOEOL NL
	| "no" TOEOL NL
	| "policy-map" WS TOEOL NL ignored_indent
	| "port-channel" TOEOL NL
	| "power" TOEOL NL
	| "redundancy" TOEOL NL ignored_indent
	| "resource" TOEOL NL
	| "rmon" TOEOL NL
	| "route-map" TOEOL NL ignored_indent
	| "router" TOEOL NL ignored_indent
	| "rtr" TOEOL NL
	| "security" TOEOL NL
	| "service" TOEOL NL
	| "snmp-server" TOEOL NL
	| "snmp" TOEOL NL
	| "spanning-tree" TOEOL NL
	| "system" TOEOL NL
	| "transceiver" TOEOL NL ignored_indent
	| "username" TOEOL NL
	| "Using" int "out of" int "bytes" NL
	| "vlan" TOEOL NL ignored_indent
	| "vpdn" TOEOL NL ignored_indent
	| "vrf" TOEOL NL ignored_indent
	| "vtp" TOEOL NL
	| "wism" TOEOL NL
	| NL
	;

ip_ignored
	= "cef" NL
	| "inspect" TOEOL NL
	| "domain" TOEOL NL
	| "name-server" TOEOL NL
	| "route" TOEOL NL
	| "http" TOEOL NL
	| "nat" TOEOL NL
	| "subnet-zero" NL
	| "dhcp excluded-address" TOEOL NL
	| "dhcp pool" {?/[^(!|\n)]*/? NL}*
	| "tcp" TOEOL NL
	| "ssh" TOEOL NL
	| "classless" NL
	| "forward-protocol" TOEOL NL
	| "ips" TOEOL NL
	| "flow-cache" TOEOL NL
	| "flow-export" TOEOL NL
	| "vrf" TOEOL NL ignored_indent
	| "multicast-routing" TOEOL NL
	| "accounting-threshold" TOEOL NL
	| "host" TOEOL NL
	| "ftp" TOEOL NL
	| "default-gateway" TOEOL NL
	| "sla" TOEOL NL ignored_indent
	| "slb" TOEOL NL ignored_indent
	| "flow-aggregation" TOEOL NL ignored_indent
	| "flow-top-talkers" TOEOL NL ignored_indent
	| "prefix-list" TOEOL NL
	| "pim" TOEOL NL
	;