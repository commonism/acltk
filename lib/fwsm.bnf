#include :: "ip.bnf"
#include :: "acl.bnf"

grammar
    =
    @:{command}+ ("pager"|$)
    ;

version
	= version:("ASA Version" ?/[^\n]*/?) NL "!" NL
	| version:("FWSM Version" ?/[^\n]*/?) NL "!" NL
	;

command
	= version
	| hostname
	| domain_name
	| interface
	| names
	| time_range
	| object
	| object_group
	| access_list
	| ignored NL
	| unmatched NL
	;

names
	= "names" NL objects:{name}* ["!" NL]
	;

name
	= "name" address:(ip4|ip6) hostname:identifier ["description" description:?/[^\n]+/?] NL
	;

access_list_rule
	= "access-list" id:acl_id line:acl_line extended:acl_extended mode:acl_mode protocol:protocol_icmp source:acl_icmp_node dest:acl_icmp_node icmp:acl_icmp_options options:acl_options NL
	| "access-list" id:acl_id line:acl_line extended:acl_extended mode:acl_mode protocol:acl_protocol source:node dest:node options:acl_options NL
	| "access-list" id:acl_id line:acl_line protocol:"ethertype" "permit" "bpdu" NL
	;

access_list
	= remark:{access_list_remark}+ ~ >access_list_rule
	| >access_list_rule
	;

access_list_remark
	= 'access-list' acl_id acl_line 'remark' remark:remark NL
	;


ignored
	= ":" ?/[^\n]*/?
	| "firewall" ?/[^\n]*/?
	| "enable"  ?/[^\n]*/?
	| "dns-guard" ?/[^\n]*/? NL "!"
	| "dns" ?/[^\n]*/?
	| "name-server" ?/[^\n]*/?
	| "domain-name" ?/[^\n]*/?
	| ["no"] "logging" ?/[^\n]*/?
	| "passwd" ?/[^\n]*/?
	| "!"
	| "same-security-traffic"  ?/[^\n]*/?
	;

unmatched
	= ()
	;
