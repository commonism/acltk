#include :: "ip.bnf"
#include :: "acl.bnf"

grammar
    =
    @:{command}+ ("pager"|$)
    ;

version
	= version:("ASA Version" ?/[^\n]*/?) NL "!" NL
	| version:("FWSM Version" ?/[^\n]*/?) NL "!" NL
	;

command
	= version
	| hostname
	| domain_name
	| interface
	| names
	| time_range
	| object
	| object_group
	| access_list
	| ignored NL
	| unmatched NL
	;

names
	= "names" NL objects:{name}* ["!" NL]
	;

name
	= "name" WS address:(ip4|ip6) WS hostname:identifier [WS "description" description:?/[^\n]+/?] NL
	;

access_list_rule
	= "access-list" WS id:acl_id WS extended:acl_extended WS mode:acl_mode WS protocol:protocol_icmp WS source:acl_icmp_node WS dest:acl_icmp_node icmp:acl_icmp_options [WS options:acl_options] NL
	| "access-list" WS id:acl_id WS extended:acl_extended WS mode:acl_mode WS protocol:acl_protocol WS source:node WS dest:node [WS options:acl_options] NL
	| "access-list" WS id:acl_id WS protocol:"ethertype" "permit" "bpdu" NL
	;

access_list
	= remark:{access_list_remark}+ >access_list_rule
	| >access_list_rule
	;

access_list_remark
	= 'access-list' WS acl_id WS 'remark' SP remark:remark NL
	;


ignored
	= ":" ?/[^\n]*/?
	| "firewall" ?/[^\n]*/?
	| "enable"  ?/[^\n]*/?
	| "dns-guard" ?/[^\n]*/? NL "!"
	| "dns" ?/[^\n]*/?
	| "name-server" ?/[^\n]*/?
	| "domain-name" ?/[^\n]*/?
	| ["no"] "logging" ?/[^\n]*/?
	| "passwd" ?/[^\n]*/?
	| "!"
	| "same-security-traffic"  ?/[^\n]*/?
	;

unmatched
	= ()
	;
