#include :: "ip.bnf"
#include :: "acl.bnf"

@override
network_group_object
	= type:`network-object` name:"host" WS address:(acl_name_ws|ip4|ip6) [WS] NL
	| type:`network-object` name:"range" WS address:(acl_name_ws|ip4|ip6) WS address:(acl_name_ws|ip4|ip6) [WS] NL
	| type:`network-object` name:(acl_name_ws|ip4) WS netmask:ip4 [WS] NL
	| type:`network-object` name:(acl_name_slash|ip6) "/" netmask:int [WS] NL
	| type:"group-object" WS group:acl_object_group_network NL
	| type:`None`
	;

@override
service_group_object
	= type:`service-object` protocol:protocol_icmp [WS icmp_type:icmp_type [WS icmp_code:icmp_code_int]] [WS] NL
	| type:`service-object` protocol:protocol_tcp_udp [WS] src:service_object_source [WS] dst:service_object_destination [WS] NL
	| type:`service-object` protocol:(protocol_code|protocol_int) [WS] NL
    | type:"group-object" WS group:acl_object_group_service [WS] NL
	| type:`None` ()
	;


grammar
	=
	@:{command ~}+ ("line"|"end"|$)
	;

version
	= "version" version:TOEOL NL
	;

command
	= version
	| hostname
	| domain_name
	| interface
	| object_group
	| access_list
	| ip
	| ipv6
	| ignored
	;

interface_detail
	= type:"description" WS value:TOEOL NL
	| type:("ip" WS "address") WS value:(ip4 WS ip4 [WS "secondary"]) [WS] NL
	| type:("ip" WS "access-group") WS value:(obj_name WS ("in"|"out")) [WS] NL
	| type:"switchport" WS value:switchport_cmd [WS] NL
	| type:`None` TOEOL NL
	| type:`None` ()
	;

banner
	= "banner" WS banner_cmd
	;


banner_cmd
	= "motd" WS delim_start delim_msg delim_stop NL
	;


delim_start
	= "^C"
	| ?/./?
	;

delim_msg
	= ()
	;

delim_stop
	= ()
	;

ipv6
    = "ipv6" WS cmd:ipv6_command
    ;

ipv6_command
    = cmd:`"access-list"` object:ipv6_access_list
    | cmd:`ignored` object:ipv6_ignored
    ;

ipv6_ignored
    = "unicast-routing" TOEOL
    | "nd"  WS TOEOL
    | "route" WS TOEOL
    | "prefix-list" WS TOEOL
    | "ipv6" TOEOL NL ignored_indent
    ;

ipv6_access_list
    = "access-list" WS name:obj_name NL objects:{ipv6_access_list_seq}+
    ;

ipv6_access_list_seq
    = SP "sequence" WS seq:int WS rule:ipv6_access_list_rule
    ;

ipv6_access_list_rule
(*
permit [ protocol ] { source-ipv6-prefix/prefix-length | any | host source-ipv6-address } [ operator
[ port-number ]] { destination-ipv6-prefix / prefix-length | any | host destination-ipv6-address
} [ operator [ port-number ]] [ dest-option-type [ doh-number | doh-type ]] [ dscp value ]
[ flow-label value ] [ fragments ] [ log ] [ log-input ] [ mobility ] [ mobility-type [ mh-number |
mh-type ]] [ routing ] [ routing-type routing-number ] [ sequence value ] [ time-range name ]

deny [ protocol ] { source-ipv6-prefix / prefix-length | any | host source-ipv6-address } [ operator
port-number ]] { destination-ipv6-prefix/prefix-length | any | host destination-ipv6-address } [
operator [ port-number ]] [ dest-option-type [ doh-number | doh-type ] ] [ dscp value ] [ flow-label
value ] [ fragments ] [ log ] [ log-input ] [ mobility ] [ mobility-type [ mh-number | mh-type ]] [
routing ] [ routing-type routing-number ] [ sequence value ] [ time-range name ] [
undetermined-transport
*)

	= mode:acl_mode WS protocol:acl_protocol WS src:ios_ipv6_node WS dst:ios_ipv6_node options:ipv6_access_list_rule_options NL
	| mode:acl_mode WS protocol:protocol_icmp WS src:ios_ipv6_node WS dst:ios_ipv6_node options:ipv6_access_list_rule_icmp_options NL
	;

ipv6_access_list_rule_option
	= type:"dest-option-type" WS value:identifier
	| type:"dscp" WS value:identifier
	| type:"flow-label" WS value:identifier
	| type:"fragments"
	| type:"log"
	| type:"log-input"
	| type:"mobility"
	| type:"mobility-type" value:int
	| type:"routing"
	| type:"routing-type" [value:int]
	| type:"time-range" WS value:identifier
	;



ipv6_access_list_rule_options
	= {WS ipv6_access_list_rule_option}*
	;

ipv6_access_list_icmp_rule_option
    = type:"nd-ns"
    | type:"nd-na"
    | type:"router-solicitation"
    | type:"router-advertisement"
    | type:"redirect"
    | type:icmp_type_name
    ;

ipv6_access_list_rule_icmp_options
    = {WS ipv6_access_list_icmp_rule_option}*
    ;

ip
	= "ip" WS cmd:ip_command
	;

ip_command
	= cmd:"access-list" WS object:ip_access_list
	| cmd:"domain name" WS name:obj_name
	| cmd:"route" WS route:ip_route
	| cmd:`None` ip_ignored
	;

(*
	ip route [vrf vrf-name] prefix mask {ip-address | interface-type interface-number [ip-address]} [dhcp] [distance] [name next-hop-name] [permanent | track number] [tag tag]
	no ip route [vrf vrf-name] prefix mask {ip-address | interface-type interface-number [ip-address]} [dhcp] [distance] [name next-hop-name] [permanent | track number] [tag tag]
*)

ip_route
	= prefix:`None` "profile" NL
	| prefix:ip4 WS mask:ip4 WS gw:(ip4|acl_interface_id) NL
	;

ip_access_list
	(* ip access-list { { standard | extended } { access-list-name | access-list-number } | helper egress check } *)
	= type:"standard" WS name:obj_name NL objects:{ip_access_list_standard}*
	| type:"extended" WS name:obj_name NL objects:{ip_access_list_extended}*
	;

ip_access_list_standard_rule_option
	= type:"log" [WS value:identifier]
	;

ip_access_list_standard_rule_options
	= {WS ip_access_list_standard_rule_option}*
	;

ip_access_list_remark
	= [seq:int WS] "remark" SP remark:remark NL
	;

ip_access_list_standard_rule
	= [seq:int WS] mode:acl_mode WS src:ios_host options:ip_access_list_standard_rule_options NL
	;

ios_host
	= type:`wildcard` address:ip4 WS wildcard:ip4
	| type:`host` address:ip4
	| type:"host" WS address:ip4
	| type:`object-group` "object-group" WS group:acl_object_group_network
	| type:`any` address:"any"
	;

ios_node
	= host:ios_host [WS port:acl_port]
	;

ios_ipv6_host
    = address:ip6 "/" prefix:int
    | "host" WS address:ip6
    | address:"any"
    ;

ios_ipv6_node
    = host:ios_ipv6_host [WS port:acl_port]
    ;

ip_access_list_standard
	= remark:{SP ip_access_list_remark}+ SP >ip_access_list_standard_rule
	| SP >ip_access_list_standard_rule
	;

ip_access_list_extended_rule_option
	= type:"precedence" WS value:identifier
	| type:"dscp" WS value:identifier
	| type:"tos" WS value:identifier
	| type:"time-range" WS value:identifier
	| type:"fragments"
	| type:"log-input" [WS value:identifier]
	| type:"log" [WS value:identifier]
	| type:"established"
	;

ip_access_list_extended_rule_options
	= {WS ip_access_list_extended_rule_option}*
	;

ip_access_list_extended_rule
	= [seq:int WS] mode:acl_mode WS protocol:protocol_icmp WS src:ios_host WS dst:ios_host icmp:acl_icmp_options options:ip_access_list_extended_rule_options NL
	| [seq:int WS] mode:acl_mode WS protocol:acl_protocol WS src:ios_node WS dst:ios_node options:ip_access_list_extended_rule_options NL
	| [seq:int WS] mode:acl_mode WS protocol:acl_protocol WS src:ios_host WS dst:ios_host options:ip_access_list_extended_rule_options NL
	;

ip_access_list_extended
	= remark:{SP ip_access_list_remark}+ SP >ip_access_list_extended_rule
	| SP >ip_access_list_extended_rule
	;

host_wildcard
	= host:ip4 [WS wildcard:ip4]
	;

(* access-list access-list-number { deny | permit } source [source-wildcard] [ log [word] ]  *)
access_list_ip_standard_rule
	=  "access-list" WS id:access_list_ip_standard_id WS mode:acl_mode WS src:ios_host options:ip_access_list_standard_rule_options NL
	;

access_list_ip_standard
	= remark:{access_list_remark}+ >access_list_ip_standard_rule
	| >access_list_ip_standard_rule
	;

access_list_remark
	= 'access-list' WS acl_id WS 'remark' WS remark:remark NL
	;

access_list
	= access_list_ip_extended
	| access_list_ip_standard
	;

(*
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } protocol source source-wildcard destination destination-wildcard [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } icmp source source-wildcard destination destination-wildcard [ icmp-type [icmp-code] | icmp-message ] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } igmp source source-wildcard destination destination-wildcard [igmp-type] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } tcp source source-wildcard [ operator [port] ] destination destination-wildcard [ operator [port] ] [established] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
	access-list access-list-number [ dynamic dynamic-name [ timeout minutes ] ] { deny | permit } udp source source-wildcard [ operator [port] ] destination destination-wildcard [ operator [port] ] [ precedence precedence | dscp dscp | tos tos | time-range time-range-name | fragments | log [word] | | log-input [word] ]
*)


access_list_ip_extended_rule
	= "access-list" WS id:access_list_ip_extended_id WS mode:acl_mode WS protocol:protocol_icmp WS src:acl_icmp_node WS dst:acl_icmp_node icmp:acl_icmp_options options:ip_access_list_extended_rule_options NL
	| "access-list" WS id:access_list_ip_extended_id WS mode:acl_mode WS protocol:acl_protocol  WS src:node WS dst:node options:ip_access_list_extended_rule_options NL
	;

access_list_ip_extended
	= remark:{access_list_remark}+ >access_list_ip_extended_rule
	| >access_list_ip_extended_rule
	;

access_list_ip_standard_id
	= /[1-9][0-9]?/ &WS
	| /13[0-9][0-9]/ &WS
	;

access_list_ip_extended_id
	= /1[0-9][0-9]/ &WS
	| /2[0-6][0-9][0-9]/ &WS
	;

switchport_cmd
    = type:"mode" WS mode:("access"|"trunk") TOEOL
    | type:"trunk" WS value:switchport_trunk_cmd TOEOL
    ;

vlan
    = type:`range` begin:int "-" end:int
    | type:`vlan` vlan:int
    ;

vlan_list
    = ",".{ vlan }+
    ;

switchport_trunk_op
    = op:"add" WS vlans:vlan_list
    | op:`set` vlans:vlan_list
    ;

switchport_trunk_cmd
    = type:"allowed" WS "vlan" WS >switchport_trunk_op
    ;

ignored_indent
	= {/^ [^\n]*/ NL}*
	;


ignored
	= "!" TOEOL NL
	| ":" TOEOL NL
	| "aaa" TOEOL NL
	| "alias" TOEOL NL
	| "appfw" TOEOL NL ignored_indent
	| "archive" TOEOL NL ignored_indent
	| "arp" TOEOL NL
	| banner
	| ("boot-start-marker"|"boot-end-marker") NL
	| "boot" TOEOL NL
	| "bridge" TOEOL NL
	| "call-home" TOEOL NL ignored_indent
	| "class-map" WS TOEOL NL ignored_indent
	| "clock" TOEOL NL
	| "control-plane" NL ignored_indent
	| "crypto" TOEOL NL ignored_indent
	| "cts" TOEOL NL
	| "Current configuration" TOEOL NL
	| "define" WS "interface-range" TOEOL NL
	| "diagnostic" TOEOL NL
	| "dialer-list" TOEOL NL
	| "dial-peer" TOEOL NL
	| "dot11" TOEOL NL ignored_indent
	| "enable" TOEOL NL
	| "encryption" TOEOL NL
	| "errdisable" TOEOL NL
	| "file" TOEOL NL
	| "firewall" TOEOL NL
	| "hw-module" TOEOL NL
	| "isdn" TOEOL NL
	| "key" TOEOL NL ignored_indent
	| "license" TOEOL NL
	| "lldp" TOEOL NL
	| "logging" TOEOL NL
	| "login" TOEOL NL
	| "mac-address-table" TOEOL NL
	| "mac-address" TOEOL NL
	| "memory" TOEOL NL
	| "mls" TOEOL NL
	| "mmi" TOEOL NL
	| "multilink" TOEOL NL
	| "no" TOEOL NL
	| "platform" WS TOEOL NL
	| "policy-map" WS TOEOL NL ignored_indent
	| "port-channel" TOEOL NL
	| "power" TOEOL NL
	| "redundancy" TOEOL NL ignored_indent
	| "resource" TOEOL NL
	| "rmon" TOEOL NL
	| "route-map" TOEOL NL ignored_indent
	| "router" TOEOL NL ignored_indent
	| "rtr" TOEOL NL
	| "security" TOEOL NL
	| "switch" TOEOL NL
	| "service" TOEOL NL
	| "snmp-server" TOEOL NL
	| "snmp" TOEOL NL
	| "spanning-tree" TOEOL NL
	| "system" TOEOL NL
	| "transceiver" TOEOL NL ignored_indent
	| "username" TOEOL NL
	| "Using" int "out of" int "bytes" NL
	| "vlan" TOEOL NL ignored_indent
	| "vpdn" TOEOL NL ignored_indent
	| "vrf" TOEOL NL ignored_indent
	| "vtp" TOEOL NL
	| "wism" TOEOL NL
	| NL
	;

ip_ignored
	= "cef" NL
	| "as-path" TOEOL NL
	| "inspect" TOEOL NL
	| "domain" TOEOL NL
	| "name-server" TOEOL NL
	| "route" TOEOL NL
	| "http" TOEOL NL
	| "nat" TOEOL NL
	| "subnet-zero" NL
	| "dhcp excluded-address" TOEOL NL
	| "dhcp pool" {?/[^(!|\n)]*/? NL}*
	| "tcp" TOEOL NL
	| "scp" TOEOL NL
	| "ssh" TOEOL NL ignored_indent
	| "classless" NL
	| "forward-protocol" TOEOL NL
	| "ips" TOEOL NL
	| "flow-cache" TOEOL NL
	| "flow-export" TOEOL NL
	| "vrf" TOEOL NL ignored_indent
	| "multicast-routing" TOEOL NL
	| "accounting-threshold" TOEOL NL
	| "host" TOEOL NL
	| "ftp" TOEOL NL
	| "default-gateway" TOEOL NL
	| "sla" TOEOL NL ignored_indent
	| "slb" TOEOL NL ignored_indent
	| "flow-aggregation" TOEOL NL ignored_indent
	| "flow-top-talkers" TOEOL NL ignored_indent
	| "prefix-list" TOEOL NL
	| "pim" TOEOL NL
	| "routing" TOEOL NL
	| "tftp" TOEOL NL
	;
