#include :: "ip.bnf"

grammar
	= @:expr $
	;

string = ?/[A-Za-z0-9_\-\.]+/?;

pattern = ?/[A-Za-z0-9_\-\.\?\*\[\]\!\/]+/?;

int = ?/[0-9]+/?;

comments
	= {comment}+
	;

comment
	= "/*" ?/(?:[^*]|(?:\*[^/]))*/? "*/"
	;

op	= "union"
	| "intersect"
	| "except"
	;

expr
	= expr_r
	| set
	;

expr_r
	= set {op (expr_r|set)}+
	;

direction
	= "src"
	| "dst"
	;

network
	= address:("ANY4"|"ANY6"|"ANY"|"any4"|"any6"|"any")
	| address:(ip4|ip6) ["/" netmask:int]
	;

net
	= object:{network}+
	;

set_ip
	= "ip" ip+:(direction net)
	;

set_id
	= "id" (pattern)
	;

set_expr
	= "(" expr ")"
	;

set_r
	= set_ip
	| set_expr
	| comments
	;

set = [comments] id:[set_id] objects:{set_r}*
	;
