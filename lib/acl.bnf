NL = ["\r"] "\n";
TOEOL = ?/[^\n]*/?;
SP = " ";
WS = /[ \t]+/;

identifier = ?/[a-zA-Z_][A-Za-z0-9_\-\.]*/?;
int = ?/[0-9]+/?;
string = ?/[\S]+/?;

obj_name = ?/[A-Za-z0-9_\-\.+]*/?;


hostname
	= "hostname" WS hostname:identifier NL
	;

domain_name
	= "domain-name" WS domain_name:identifier
	;


description
	= "description" WS description:?/[^\n]+/? NL
	;


interface
	= "interface" WS alias:interface_alias NL detail:{SP interface_detail}
	;


interface_alias
	= ?/[^\s]+/?
	;

acl_id
	= obj_name
	;

acl_line
	= "line" int
	| ()
	;

acl_extended
	= "extended"
	;

acl_mode
	= ("permit"|"deny")
	;

acl_protocol
	= type:"object-group" WS name:(acl_object_group_service_id|acl_object_group_protocol_id)
	| type:"object" WS name:acl_object_service_id
	| name:(protocol_code|int)
	;

node
	= host:acl_host [WS port:acl_port]
	;

acl_object_group_network_id
	= !()
	;

acl_object_network_id
	= !()
	;

acl_object_group_service_id
	= !()
	;

acl_object_service_id
	= !()
	;

acl_object_group_icmp_id
	= !()
	;

acl_object_group_protocol_id
	= !()
	;

acl_names_id
	= !()
	;

acl_time_range_id
	= !()
	;

acl_interface_id
	= !()
	;

acl_host
	= type:("host"|"ip") WS address:(acl_names_id|ip4|ip6)
	| type:"interface" WS name:identifier
	| type:("any"|"any4"|"any6")
	| type:"object" WS name:acl_object_network_id
	| type:"object-group" WS name:acl_object_group_network_id
	| address:(acl_names_id|ip4) WS netmask:ip4
	| address:(acl_names_id|ip6) "/" netmask:int
	;

acl_port
	= type:"object-group" WS name:acl_object_group_service_id
	| op:("gt"|"lt"|"eq"|"neq") WS port:port
	| type:"range" WS start:port WS stop:port
	;

acl_icmp_options
	= WS type:"object-group" WS object:acl_object_group_icmp_id
	| WS type:(icmp_type|int) [WS code:int]
	| ()
	;

acl_options
	= {acl_option [WS]}
	;

acl_option
	= type:"log" options:{WS acl_option_log_option}*
	| type:"time-range" WS option:acl_time_range_id
	| type:"inactive"
	| ()
	;


(* [log [[disable|default]|level] interval secs] *)
(* access-list access_list_name [extended] {deny|permit}...[log [[level] [interval secs] |disable|default]] *)

acl_option_log_option
	= ("debugging"|"disable"|"default"|"notifications"|"warnings")
	| "interval" WS int
	| ("0"|"1"|"2"|"3"|"4"|"5"|"6"|"7")
	| ()
	;

acl_icmp_node
	= acl_host
	;

remark
	= (?/[^\n]*/?)
	;

object
	= "object" WS object_type
	;

object_type
	= type:"network" WS name:obj_name [NL SP args:network_object] [SP >description]
	| type:"service" WS name:obj_name [NL SP args:service_object] [SP >description]
	;

network_object
	= type:"host" WS address:(ip4|ip6) NL
	| type:"subnet" WS address:ip4 WS mask:ip4 NL
	| type:"subnet" WS address:ip6 "/" mask:int NL
	| type:"range" WS start:ip4 WS stop:ip4 NL
	| type:"range" WS start:ip6 WS stop:ip6 NL
	| type:"fqdn" WS [limit:("v4"|"v6") WS] fqdn:string NL
	| type:"nat" TOEOL NL
	;

service_object
	= type:"service" WS protocol:protocol_icmp [WS icmp_type:(icmp_type|int) [WS icmp_code:int]] NL
	| type:"service" WS protocol:protocol [WS] source:service_object_source [WS] destination:service_object_destination [WS] NL
	| type:"service" WS protocol:int NL
	;

service_object_op
	= ("lt"|"gt"|"eq"|"neq") WS port
	| "range" WS port WS port
	;

service_object_source
	= "source" WS @:service_object_op
	| ()
	;

service_object_destination
	= "destination" WS @:service_object_op
	| ()
	;

object_group
	= "object-group" WS object_group_type
	;

protocol_icmp
	= "icmp"
	| "icmp6"
	;

protocol
	= port_group_protocol
	;

port_group_protocol
	= ("tcp-udp"|"tcp"|"udp")
	;

object_group_type
	= type:"network" WS name:obj_name NL [SP >description] objects:{SP network_group_object}
	(* PR 8.3 *)
	| "service" WS name:obj_name WS type:port_group_protocol NL [SP >description] objects:{SP port_group_object}
	(* PR 9.3 *)
	| type:"service" WS name:obj_name NL [SP >description] objects:{SP service_group_object}
	| type:"icmp-type" WS name:obj_name NL [SP >description] objects:{SP icmp_group_object}
	| type:"protocol" WS name:obj_name NL [SP >description] objects:{SP protocol_group_object}
	;

service_group_object
	= type:"service-object" WS protocol:protocol_icmp [WS icmp_type:(icmp_type|int) [WS icmp_code:int]] NL
	| type:"service-object" WS protocol:protocol [WS] source:service_object_source [WS] destination:service_object_destination [WS] NL
	| type:"service-object" WS protocol:(protocol_code|int) NL
	| type:"service-object" WS protocol:"object" WS object:acl_object_service_id NL
	| type:"group-object" WS object:acl_object_group_service_id NL
	| ()
	;

port_group_object
	= "port-object" WS "eq" WS port NL
	| "port-object" WS "range" WS port WS port NL
	| "group-object" WS acl_object_group_service_id NL
	| ()
	;

network_group_object
	= type:"network-object" WS name:"host" WS address:(acl_names_id|ip4|ip6) NL
	| type:"network-object" WS name:"object" WS object:acl_object_network_id NL
	| type:"network-object" WS name:(acl_names_id|ip4) WS netmask:ip4 NL
	| type:"network-object" WS name:(acl_names_id|ip6) "/" netmask:int NL
	| type:"group-object" WS object:acl_object_group_network_id NL
	| ()
	;

icmp_group_object
	= type:"icmp-object" WS name:(icmp_type|int) NL
	| type:"group-object" WS name:acl_object_group_icmp_id NL
	| ()
	;



protocol_group_object
	= type:"protocol-object" WS name:obj_name NL
	| type:"group-object" WS name:acl_object_group_protocol_id NL
	| ()
	;

port
	= (port_code|int)
	;

hour
	= ?/[0-9]{1,2}/?
	;

minute
	= ?/[0-9]{1,2}/?
	;

time
	= hour:hour ":" minute:minute
	;

year
	= ?/[0-9]{4}/?
	;

day =
	?/[0-9]{1,2}/?
	;


date
	= day:day WS month:month WS year:year
	;

timedate
	= >time WS >date
	;

time_range_object
	= type:"periodic" WS days:{day_of_the_week}+ WS start:time WS "to" WS edays:[day_of_the_week WS] end:time NL
	| type:"periodic" WS days+:("weekdays"|"weekend"|"daily") WS start:time WS "to" WS end:time NL
	| type:"absolute" [WS "start" WS start:timedate] [WS "end" WS end:timedate] NL
	| type:"no" WS TOEOL NL
	;

time_range
	= 'time-range' WS name:string NL objects:{SP time_range_object}*
	;


day_of_the_week
	= "Monday"
	| "Tuesday"
	| "Wednesday"
	| "Thursday"
	| "Friday"
	| "Saturday"
	| "Sunday"
	;

month
	= "January"
	| "February"
	| "March"
	| "April"
	| "May"
	| "June"
	| "July"
	| "August"
	| "September"
	| "October"
	| "November"
	| "December"
	;

# https://www.cisco.com/c/en/us/td/docs/security/asa/asa72/configuration/guide/conf_gd/ports.html#wp1021334%0A
icmp_type
	= "unreachable"
	| "ttl-exceeded"
	| "traceroute"
	| "timestamp-request"
	| "timestamp-reply"
	| "time-exceeded"
	| "source-route-failed"
	| "source-quench"
	| "router-solicitation"
	| "router-renumbering"
	| "router-advertisement"
	| "redirect"
	| "reassembly-timeout"
	| "protocol-unreachable"
	| "precedence-unreachable"
	| "port-unreachable"
	| "parameter-problem"
	| "packet-too-big"
	| "option-missing"
	| "no-room-for-option"
	| "network-unknown"
	| "net-unreachable"
	| "net-tos-unreachable"
	| "net-tos-redirect"
	| "net-redirect"
	| "neighbor-solicitation"
	| "neighbor-redirect"
	| "neighbor-advertisement"
	| "mobile-redirect"
	| "membership-report"
	| "membership-reduction"
	| "membership-query"
	| "mask-request"
	| "mask-reply"
	| "information-request"
	| "information-reply"
	| "host-unreachable"
	| "host-unknown"
	| "host-tos-unreachable"
	| "host-tos-redirect"
	| "host-redirect"
	| "host-precedence-unreachable"
	| "host-isolated"
	| "general-parameter-problem"
	| "echo-request"
	| "echo-reply"
	| "echo"
	| "dod-net-prohibited"
	| "dod-host-prohibited"
	| "destination-unreachable"
	| "conversion-error"
	| "alternate-address"
	| "administratively-prohibited"
	| "address-mask-request"
	| "address-mask-reply"
	;


protocol_code
	= "udp"
	| "tcp"
	| "snp"
	| "pptp"
	| "pim"
	| "pcp"
	| "ospf"
	| "nos"
	| "ipsec"
	| "ipinip"
	| "ip"
	| "igrp"
	| "igmp"
	| "icmp6"
	| "icmpv6"
	| "icmp"
	| "gre"
	| "esp"
	| "eigrp"
	| "ahp"
	| "ah"
	;

port_code
	= "aol"
	| "bgp"
	| "biff"
	| "bootpc"
	| "bootps"
	| "chargen"
	| "citrix-ica"
	| "cmd"
	| "ctiqbe"
	| "daytime"
	| "discard"
	| "domain"
	| "dnsix"
	| "echo"
	| "exec"
	| "finger"
	| "ftp-data"
	| "ftp"
	| "gopher"
	| "https"
	| "h323"
	| "hostname"
	| "ident"
	| "imap4"
	| "irc"
	| "isakmp"
	| "kerberos"
	| "klogin"
	| "kshell"
	| "ldap"
	| "ldaps"
	| "lpd"
	| "login"
	| "lotusnotes"
	| "mobile-ip"
	| "nameserver"
	| "netbios-ns"
	| "netbios-dgm"
	| "netbios-ssn"
	| "netbios-ss"
	| "nfs"
	| "nntp"
	| "non500-isakmp"
	| "ntp"
	| "pcanywhere-status"
	| "pcanywhere-data"
	| "pim-auto-rp"
	| "pop2"
	| "pop3"
	| "pptp"
	| "radius"
	| "radius-acct"
	| "rip"
	| "rpc"
	| "rtsp"
	| "rsh"
	| "secureid-udp"
	| "sip"
	| "smtp"
	| "snmp"
	| "snmptrap"
	| "sqlnet"
	| "ssh"
	| "sunrpc"
	| "syslog"
	| "tacacs"
	| "talk"
	| "telnet"
	| "tftp"
	| "time"
	| "uucp"
	| "who"
	| "whois"
	| "www"
	| "xdmcp"
	;
