NL = "\n";
TOEOL = ?/[^\n]*/?;

identifier = ?/[a-zA-Z_][A-Za-z0-9_\-\.]*/?;
int = ?/[0-9]+/?;
string = ?/[\S]+/?;

obj_name = ?/[A-Za-z0-9_\-\.]*/?;


hostname
	= "hostname" hostname:identifier NL
	;

domain_name
	= "domain-name" domain_name:identifier
	;


description
	= "description" description:?/[^\n]+/? NL
	| ()
	;


interface
	= "interface" alias:interface_alias NL detail:{interface_detail} "!" NL
	;

interface_alias
	= ?/[^\s]+/?
	;

interface_detail
	= type:"nameif" name:identifier NL
	| type:"description" description:?/[^\n]+/? NL
	| type:"ip nat" name:obj_name NL
	| "no ip address" ?/[^!]*/? "!" NL
	| "encryption mode" ?/[^!]*/? "!" NL
	| "ssid" ?/[^!]*/? "!" NL
	| ?/[^(^!|\n)]*/? NL
	| ()
	;

acl_id
	= obj_name
	;

acl_line
	= "line" int
	| ()
	;

acl_extended
	= "extended"
	;

acl_mode
	= ("permit"|"deny")
	;

acl_protocol
	= type:"object-group" name:(acl_object_group_service_id|acl_object_group_protocol_id)
	| type:"object" name:acl_object_service_id
	| name:(protocol_code|int)
	;

node
	= host:acl_host [port:acl_port]
	;

acl_object_group_network_id
	= !()
	;

acl_object_network_id
	= !()
	;

acl_object_group_service_id
	= !()
	;

acl_object_service_id
	= !()
	;

acl_object_group_icmp_id
	= !()
	;

acl_object_group_protocol_id
	= !()
	;

acl_names_id
	= !()
	;

acl_time_range_id
	= !()
	;

acl_host
	= type:("host"|"ip") address:(acl_names_id|ip4|ip6)
	| type:"interface" name:identifier
	| type:("any"|"any4"|"any6")
	| type:"object" name:acl_object_network_id
	| type:"object-group" name:acl_object_group_network_id
	| address:(acl_names_id|ip4) netmask:ip4
	| address:(acl_names_id|ip6) "/" netmask:int
	;

acl_port
	= type:"object-group" name:acl_object_group_service_id
	| op:("gt"|"lt"|"eq") port:port
	| type:"range" start:port stop:port
	;

acl_icmp_options
	= type:"object-group" object:acl_object_group_icmp_id
	| type:(icmp_type|int) code:[int]
	| ()
	;

acl_options
	= {acl_option}
	;

acl_option
	= type:"log" options:{acl_option_log_option}*
	| type:"time-range" option:acl_time_range_id
	| type:"inactive"
	| ()
	;

acl_option_log_option
	= ("disable"|"default"|"notifications"|"warnings")
	| "interval" int
	| ()
	;

acl_icmp_node
	= acl_host
	;

remark
	= (?/[^\n]*/?)
	;

object
	= "object" object_type
	;

object_type
	= type:"network" name:obj_name NL args:network_object >description
	| type:"service" name:obj_name NL args:service_object >description
	;

network_object
	= type:"host" address:(ip4|ip6) NL
	| type:"subnet" address:ip4 mask:ip4 NL
	| type:"subnet" address:ip6 "/" mask:int NL
	| type:"range" start:ip4 stop:ip4 NL
	| type:"range" start:ip6 stop:ip6 NL
	| type:"fqdn" limit:[("v4"|"v6")] fqdn:string NL
	;

service_object
	= type:"service" protocol:int NL
	| type:"service" protocol:protocol_icmp icmp_type:[(icmp_type|int)] icmp_code:[int] NL
	| type:"service" protocol:protocol source:service_object_source destination:service_object_destination NL
	;

service_object_op
	= ("lt"|"gt"|"eq"|"neq") port
	| "range" port port
	;

service_object_source
	= "source" @:service_object_op
	| ()
	;

service_object_destination
	= "destination" @:service_object_op
	| ()
	;

object_group
	= "object-group" object_group_type
	;

protocol_icmp
	= "icmp"
	| "icmp6"
	;

protocol
	= port_group_protocol
	;

port_group_protocol
	= ("tcp-udp"|"tcp"|"udp")
	;

object_group_type
	= type:"network" name:obj_name NL >description objects:{network_group_object}
	(* PR 8.3 *)
	| "service" name:obj_name type:port_group_protocol NL >description objects:{port_group_object}
	(* PR 9.3 *)
	| type:"service" name:obj_name NL >description objects:{service_group_object}
	| type:"icmp-type" name:obj_name NL >description objects:{icmp_group_object}
	| type:"protocol" name:obj_name NL >description objects:{protocol_group_object}
	;

service_group_object
	= type:"service-object" protocol:int NL
	| type:"service-object" protocol:protocol_icmp icmp_type:[(icmp_type|int)] icmp_code:[int] NL
	| type:"service-object" protocol:protocol source:service_object_source destination:service_object_destination NL
	| type:"service-object" protocol:"object" object:acl_object_service_id NL
    | type:"group-object" object:acl_object_group_service_id NL
	| ()
	;

port_group_object
	= "port-object" "eq" port NL
	| "port-object" "range" port port NL
	| "group-object" acl_object_group_service_id NL
	| ()
	;

network_group_object
	= type:"network-object" name:"host" address:(acl_names_id|ip4|ip6) NL
	| type:"network-object" name:"object" object:acl_object_network_id NL
	| type:"network-object" name:(acl_names_id|ip4) netmask:ip4 NL
	| type:"network-object" name:(acl_names_id|ip6) "/" netmask:int NL
	| type:"group-object" object:acl_object_group_network_id NL
	| ()
	;

icmp_group_object
	= type:"icmp-object" name:(icmp_type|int) NL
	| type:"group-object" name:acl_object_group_icmp_id NL
	| ()
	;



protocol_group_object
	= type:"protocol-object" name:obj_name NL
	| type:"group-object" name:acl_object_group_protocol_id NL
	| ()
	;

port
	= (port_code|int)
	;

hour
	= ?/[0-9]{1,2}/?
	;

minute
	= ?/[0-9]{1,2}/?
	;

time
	= hour:hour ":" minute:minute
	;

year
	= ?/[0-9]{4}/?
	;

day =
	?/[0-9]{1,2}/?
	;


date
	= day:day month:month year:year
	;

timedate
	= >time >date
	;

time_range_object
	= type:"periodic" days:{day_of_the_week}+ start:time "to" edays:[day_of_the_week] end:time NL
	| type:"periodic" days+:("weekdays"|"weekend"|"daily") start:time "to" end:time NL
	| type:"absolute" ["start" start:timedate] ["end" end:timedate] NL
	| type:"no" any:("periodic"|"absolute") NL
	;

time_range
	= 'time-range' name:string NL objects:{time_range_object}+ "!" NL
	;


day_of_the_week
	= "Monday"
	| "Tuesday"
	| "Wednesday"
	| "Thursday"
	| "Friday"
	| "Saturday"
	| "Sunday"
	;

month
	= "January"
	| "February"
	| "March"
	| "April"
	| "May"
	| "June"
	| "July"
	| "August"
	| "September"
	| "October"
	| "November"
	| "December"
	;


icmp_type
	= "unreachable"
	| "traceroute"
	| "timestamp-request"
	| "timestamp-reply"
	| "time-exceeded"
	| "source-quench"
	| "router-solicitation"
	| "router-advertisement"
	| "redirect"
	| "parameter-problem"
	| "mobile-redirect"
	| "mask-request"
	| "mask-reply"
	| "information-request"
	| "information-reply"
	| "echo-reply"
	| "echo"
	| "ttl-exceeded"
	;

protocol_code
	= "ip"
	| "icmp"
	| "igmp"
	| "ipinip"
	| "tcp"
	| "igrp"
	| "udp"
	| "gre"
	| "pptp"
	| "esp"
	| "ipsec"
	| "ahp"
	| "ah"
	| "icmp6"
	| "eigrp"
	| "ospf"
	| "nos"
	| "pim"
	| "pcp"
	| "snp"
	;

port_code
	= "aol"
	| "bgp"
	| "biff"
	| "bootpc"
	| "bootps"
	| "chargen"
	| "citrix-ica"
	| "cmd"
	| "ctiqbe"
	| "daytime"
	| "discard"
	| "domain"
	| "dnsix"
	| "echo"
	| "exec"
	| "finger"
	| "ftp-data"
	| "ftp"
	| "gopher"
	| "https"
	| "h323"
	| "hostname"
	| "ident"
	| "imap4"
	| "irc"
	| "isakmp"
	| "kerberos"
	| "klogin"
	| "kshell"
	| "ldap"
	| "ldaps"
	| "lpd"
	| "login"
	| "lotusnotes"
	| "mobile-ip"
	| "nameserver"
	| "netbios-ns"
	| "netbios-dgm"
	| "netbios-ssn"
	| "netbios-ss"
	| "nntp"
	| "non500-isakmp"
	| "ntp"
	| "pcanywhere-status"
	| "pcanywhere-data"
	| "pim-auto-rp"
	| "pop2"
	| "pop3"
	| "pptp"
	| "radius"
	| "radius-acct"
	| "rip"
	| "rpc"
	| "rtsp"
	| "rsh"
	| "secureid-udp"
	| "sip"
	| "smtp"
	| "snmp"
	| "snmptrap"
	| "sqlnet"
	| "ssh"
	| "sunrpc"
	| "syslog"
	| "tacacs"
	| "talk"
	| "telnet"
	| "tftp"
	| "time"
	| "uucp"
	| "who"
	| "whois"
	| "www"
	| "xdmcp"
	;
