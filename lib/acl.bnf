grammar
    =
    @:{command}+ ("pager"|$)
    ;

#include :: "ip.bnf"

identifier = ?/[a-zA-Z_][A-Za-z0-9_\-\.]*/?;
int = ?/[0-9]+/?;
string = ?/[\S]+/?;

obj_name = ?/[A-Za-z0-9_\-\.]*/?;

command 
	= hostname
	| domain_name
	| interface
	| names
	| time_range
	| object
	| object_group
	| access_list_remarks
	| access_list
	| ignored
	| unmatched
	;

description
	= "description" description:?/[^\n]+/?
	| ()
	;

hostname
	= "hostname" hostname:identifier;

domain_name
	= "domain-name" domain_name:identifier
	;

interface
	= "interface" alias:interface_alias detail:{interface_detail} "!"
	;

interface_alias
	= ?/[^\s]+/?
	;

interface_detail
	= "nameif" name:identifier
	| "bridge-group" bridge_group:(identifier|int)
	| "security-level" security_level:int
	| "ip address" addr:ip4 netmask:ip4 any:?/[^\n]+/?
	| "no" any:?/[^\n]+/?
	| "ipv6 address" addr:ip6 "/" netmask:int any:?/[^\n]+/?
	| "vlan" vlan:identifier
	| "description" description:?/[^\n]+/?
	| "ipv6 nd" any:?/[^\n]+/?
	| "ipv6 enable"
	| "management-only"
	| ()
	;

names
	= "names" objects:{name}* ["!"]
	;

name 
	= "name" address:(ip4|ip6) hostname:identifier >description
	;

day_of_the_week
	= "Monday"
	| "Tuesday"
	| "Wednesday"
	| "Thursday"
	| "Friday"
	| "Saturday"
	| "Sunday"
	;

month
	= "January"
	| "February"
	| "March"
	| "April"
	| "May"
	| "June"
	| "July"
	| "August"
	| "September"
	| "October"
	| "November"
	| "December"
	;


hour
	= ?/[0-9]{1,2}/?
	;

minute
	= ?/[0-9]{1,2}/?
	;

time
	= hour:hour ":" minute:minute
	;

year
	= ?/[0-9]{4}/?
	;

day = 
	?/[0-9]{1,2}/?
	;


date
	= day:day month:month year:year
	;

timedate
	= >time >date
	;

time_range_object
	= type:"periodic" days:{day_of_the_week}+ start:time "to" edays:[day_of_the_week] end:time
	| type:"periodic" days+:("weekdays"|"weekend"|"daily") start:time "to" end:time
	| type:"absolute" ["start" start:timedate] ["end" end:timedate]
	| type:"no" any:("periodic"|"absolute")
	;

time_range
	= 'time-range' name:string objects:{time_range_object}+ "!"
	;

object
	= "object" object_type
	;

object_type
	= type:"network" name:obj_name args:network_object >description
	| type:"service" name:obj_name args:service_object >description
	;

network_object
	= type:"host" address:(ip4|ip6)
	| type:"subnet" address:ip4 mask:ip4
	| type:"subnet" address:ip6 "/" mask:int
	| type:"range" start:ip4 stop:ip4
	| type:"range" start:ip6 stop:ip6
	| type:"fqdn" limit:[("v4"|"v6")] fqdn:string
	;

service_object
	= type:"service" protocol:int
	| type:"service" protocol:protocol_icmp icmp_type:[(icmp_type|int)] icmp_code:[int]
	| type:"service" protocol:protocol source:service_object_source destination:service_object_destination
	;

service_object_op
	= ("lt"|"gt"|"eq"|"neq") port
	| "range" port port
	;

service_object_source
	= "source" @:service_object_op
	| ()
	;

service_object_destination
	= "destination" @:service_object_op
	| ()
	;

port
	= (port_code|int)
	;

object_group
	= "object-group" object_group_type
	;

protocol_icmp
	= "icmp"
	| "icmp6"
	;

protocol
	= _protocol
	;

_protocol
	= ("tcp-udp"|"tcp"|"udp")
	;

object_group_type
	= type:"network" name:obj_name >description objects:{network_group_object}
	(* PR 8.3 *)
	| "service" name:obj_name type:_protocol >description objects:{port_group_object}
	(* PR 9.3 *)
	| type:"service" name:obj_name >description objects:{service_group_object}
	| type:"icmp-type" name:obj_name >description objects:{icmp_group_object}
	| type:"protocol" name:obj_name >description objects:{protocol_group_object}
	;

service_group_object
	= type:"service-object" protocol:int
	| type:"service-object" protocol:protocol_icmp icmp_type:[(icmp_type|int)] icmp_code:[int]
	| type:"service-object" protocol:protocol source:service_object_source destination:service_object_destination
	| type:"service-object" protocol:"object" object:acl_object_service_id
    | type:"group-object" object:acl_object_group_service_id
	| ()
	;

port_group_object
	= "port-object" "eq" port
	| "port-object" "range" port port
	| "group-object" acl_object_group_service_id 
	| ()
	;

network_group_object
	= type:"network-object" name:"host" address:(acl_names_id|ip4|ip6)
	| type:"network-object" name:"object" object:acl_object_network_id
	| type:"network-object" name:(acl_names_id|ip4) netmask:ip4
	| type:"network-object" name:(acl_names_id|ip6) "/" netmask:int
	| type:"group-object" object:acl_object_group_network_id
	| ()
	;

icmp_group_object
	= type:"icmp-object" name:(icmp_type|int)
	| type:"group-object" name:acl_object_group_icmp_id
	| ()
	;



protocol_group_object
	= type:"protocol-object" name:obj_name
	| type:"group-object" name:acl_object_group_protocol_id
	| ()
	;

acl_id
	= obj_name
	;

acl_line
	= "line" int
	| ()
	;

acl_extended
	= "extended"
	;

acl_mode
	= ("permit"|"deny")
	;

acl_protocol
	= type:"object-group" name:(acl_object_group_service_id|acl_object_group_protocol_id)
	| type:"object" name:acl_object_service_id
	| name:(protocol_code|int)
	;

acl_source
	= host:acl_host [port:acl_port]
	;

acl_dest
	= host:acl_host [port:acl_port]
	;

acl_object_group_network_id
	= !()
	;

acl_object_network_id
	= !()
	;

acl_object_group_service_id
	= !()
	;

acl_object_service_id
	= !()
	;

acl_object_group_icmp_id
	= !()
	;

acl_object_group_protocol_id
	= !()
	;

acl_names_id
	= !()
	;

acl_time_range_id
	= !()
	;

acl_host
	= type:("host"|"ip") address:(acl_names_id|ip4|ip6)
	| type:"interface" name:identifier
	| type:("any"|"any4"|"any6")
	| type:"object" name:acl_object_network_id
	| type:"object-group" name:acl_object_group_network_id
	| address:(acl_names_id|ip4) netmask:ip4
	| address:(acl_names_id|ip6) "/" netmask:int
	;

acl_port
	= type:"object-group" name:acl_object_group_service_id
	| op:("gt"|"lt"|"eq") port:port
	| type:"range" start:port stop:port
	;

acl_icmp_options
	= type:"object-group" object:acl_object_group_icmp_id
	| type:(icmp_type|int) code:[int]
	| ()
	;

acl_options
	= {acl_option}
	;

acl_option
	= type:"log" options:{acl_option_log_option}*
	| type:"time-range" option:acl_time_range_id
	| type:"inactive"
	| ()
	;

acl_option_log_option
	= ("disable"|"default"|"notifications"|"warnings")
	| "interval" int
	| ()
	;

acl_icmp_node
	= acl_host
	;
	
access_list
	= "access-list" id:acl_id line:acl_line extended:acl_extended mode:acl_mode protocol:protocol_icmp source:acl_icmp_node dest:acl_icmp_node icmp:acl_icmp_options options:acl_options
	| "access-list" id:acl_id line:acl_line extended:acl_extended mode:acl_mode protocol:acl_protocol source:acl_source dest:acl_dest options:acl_options
	| "access-list" id:acl_id line:acl_line protocol:"ethertype" "permit" "bpdu"
	;

remark
	= (?/[^\n]*/?)
	;

access_list_remarks
	= remark:{access_list_remark}+ ~ >access_list
	;

access_list_remark
	= 'access-list' acl_id acl_line 'remark' remark:remark
	;


ignored
	= ":" ?/[^\n]*/?
	| "ASA Version" ?/[^\n]*/? "!"
	| "FWSM Version" ?/[^\n]*/? "!"
	| "firewall" ?/[^\n]*/?
	| "enable"  ?/[^\n]*/?
	| "dns-guard" ?/[^\n]*/? "!"
	| "dns" ?/[^\n]*/?
	| "name-server" ?/[^\n]*/?
	| "domain-name" ?/[^\n]*/?
	| ["no"] "logging" ?/[^\n]*/?
	| "passwd" ?/[^\n]*/?
	| "!"
	| "same-security-traffic"  ?/[^\n]*/?
	;

unmatched
	= ()
	;

icmp_type
	= "unreachable"
	| "traceroute"
	| "timestamp-request"
	| "timestamp-reply"
	| "time-exceeded"
	| "source-quench"
	| "router-solicitation"
	| "router-advertisement"
	| "redirect"
	| "parameter-problem"
	| "mobile-redirect"
	| "mask-request"
	| "mask-reply"
	| "information-request"
	| "information-reply"
	| "echo-reply"
	| "echo"
	;

protocol_code
    = "ip"  
    | "icmp" 
    | "igmp" 
    | "ipinip"
    | "tcp" 
    | "igrp" 
    | "udp" 
    | "gre" 
    | "pptp" 
    | "esp" 
    | "ipsec"
    | "ah"  
    | "icmp6"
    | "eigrp"
    | "ospf" 
    | "nos" 
    | "pim" 
    | "pcp" 
    | "snp" 
	;

port_code
	= "aol"
	| "bgp"
	| "biff"
	| "bootpc"
	| "bootps"
	| "chargen"
	| "citrix-ica"
	| "cmd"
	| "ctiqbe"
	| "daytime"
	| "discard"
	| "domain"
	| "dnsix"
	| "echo"
	| "exec"
	| "finger"
	| "ftp-data"
	| "ftp"
	| "gopher"
	| "https"
	| "h323"
	| "hostname"
	| "ident"
	| "imap4"
	| "irc"
	| "isakmp"
	| "kerberos"
	| "klogin"
	| "kshell"
	| "ldap"
	| "ldaps"
	| "lpd"
	| "login"
	| "lotusnotes"
	| "mobile-ip"
	| "nameserver"
	| "netbios-ns"
	| "netbios-dgm"
	| "netbios-ssn"
	| "nntp"
	| "ntp"
	| "pcanywhere-status"
	| "pcanywhere-data"
	| "pim-auto-rp"
	| "pop2"
	| "pop3"
	| "pptp"
	| "radius"
	| "radius-acct"
	| "rip"
	| "rpc"
	| "rtsp"
	| "rsh"
	| "secureid-udp"
	| "sip"
	| "smtp"
	| "snmp"
	| "snmptrap"
	| "sqlnet"
	| "ssh"
	| "sunrpc"
	| "syslog"
	| "tacacs"
	| "talk"
	| "telnet"
	| "tftp"
	| "time"
	| "uucp"
	| "who"
	| "whois"
	| "www"
	| "xdmcp"
	;

